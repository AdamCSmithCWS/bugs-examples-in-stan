Turnout: logit model for binary data

Voter turnout is coded as a binary indicator (y =1 if voted, 0 otherwise) and related to covariates via a logit model.  The data comprise a random 3,000 observation subset of a much larger data set analyzed by Jonathan Nagler.


model{
  for (i in 1:N){                 ## loop over observations
    y[i] ~ dbern(p[i]);           ## binary outcome, Bernoulli trial
    logit(p[i]) <- mu[i];         ## logit link
    mu[i] <- beta[1]              ## regression structure for covariates
             + beta[2]*educ[i]
             + beta[3]*(educ[i]*educ[i])      
             + beta[4]*age[i]
             + beta[5]*(age[i]*age[i])
             + beta[6]*south[i]
             + beta[7]*govelec[i]
             + beta[8]*closing[i] 
             + beta[9]*(closing[i]*educ[i]) 
             + beta[10]*(educ[i]*educ[i]*closing[i]);
		
		## not necc for model fitting
    llh[i] <- y[i]*log(p[i]) + (1-y[i])*log(1-p[i]);  ## llh contribution
  }

  sumllh <- sum(llh[]);    ## sum of log-likelihood contributions
  
  ## priors, b0 and B given at end of data file, imply vague priors
  beta[1:10] ~ dmnorm(b0[ ] , B[ , ]) ;  ## multivariate Normal prior
}

## reasonable start values
list(beta = c(0, 0, 0, 0, 0, 0, 0, 0, 0, 0))

## alternative set of start values for parallel chains, from OLS
list(beta=c(-0.3392423378,  0.0741911953,  0.0012163747,  0.0230970246, 
            -0.0001679677, -0.0333484965,  0.0204799754, -0.0068319918,                0.0017752978, -0.0001432201)