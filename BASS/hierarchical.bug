model{
	for(i in 1:n){          ## loop over observations
	      for(j in 1:5){    ## loop over indicators
                    mu[i,j] <- gammaTmp[j,1] + gammaTmp[j,2]*xiTmp[i]
		    y[i,j] ~ dnorm(mu[i,j],tau[j])
		    }
	}

	for(i in 1:n){         ## hierarchical model for x
	      muxi[i] <- alphaTmp[south[i]+1]  ## select alpha for this obs
	      xiTmp[i] ~ dnorm(muxi[i],1)
        }
	
	for(j in 1:5){        ## priors for measurement parameters
	      gammaTmp[j,1:2] ~ dmnorm(g0[1:2],G0[1:2,1:2])
              tau[j] ~ dgamma(.01,.01)
	      omega[j] <- 1/sqrt(tau[j])
	}		    
	
	for(j in 1:2){ 	      ## priors over hierarchical model
	      alphaTmp[j] ~ dnorm(0,1)
        }

	## transformations to identified parameters
	m <- sd(xiTmp[1:n])
	c <- mean(xiTmp[1:n])
	for(i in 1:n){
	      xi[i] <- (xiTmp[i]-c)/m
	}
	for(j in 1:5){
	      gamma[j,1] <- gammaTmp[j,1] + gammaTmp[j,2]*c/m
	      gamma[j,2] <- gammaTmp[j,2]*m
	}
	for(j in 1:2){
	      alpha[j] <- (alphaTmp[j]-c)/m
	}	      
	## difference in average xi, between south and non-south
	delta <- alpha[1] - alpha[2]
}
