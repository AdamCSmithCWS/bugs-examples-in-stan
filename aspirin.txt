Aspirin: Shrinkage (or "borrowing strength") via hierarchical modeling

The following data come from a meta-analysis of heart attack data.  Each observation is the results of a study of survivorship following a heart attack (myocardial infarction). In each study, some victims were given aspirin immediately following their heart attack, while some vicitims were not.  The observed values of y are the differences in mean survivorship observed in each study, with the other piece of data, the standard deviations, reflecting the relative sizes of the two groups in each study (i.e., although the data are binomial, given the large number of observations per study a normal approximation is valid and reduces each study's data to the observed treatement effect and a standard deviation).  The goal of the meta-analysis is to synthesize the six studies, so as to arrive at an overall conclusion regarding the effects of aspirin on survivorship following a heart attack.
 
This is an extremely simple example of hierarchical modeling.  Via the exchangeability assumption (i.e., the study-specific means have a common prior), the studies "borrow strength" from one another, introducing some bias (each study's mean qi is shrunk back towards the common mean), but with the benefit of gaining precision (smaller variance).  We also gain a better estimate of the overall effect of aspirin on survivorship after heart attack than we would get from naively pooling the studies.

These data and the meta-analysis is discussed at length in David Draper et al. (1992), Combining Information: Statistical Issues and Opportunities for Research, American Statistical Association: Alexandria, Virginia.

model{
	for(i in 1:6){                    ## loop over studies
		theta[i] ~ dnorm(mu,tau);       ## prior for each study
		v[i] <- pow(sd[i],2);           ## convert each study's se to var
		precision[i] <- 1/v[i];         ## convert var to precision
		y[i] ~ dnorm(theta[i],precision[i]);  ## model for each study
		b[i] <- v[i]/(v[i] + sigma2)    ## shrinkage (auxilary quantity)
		}
	mu ~ dnorm(0.0, .001);            ## prior for the common mean
	tau ~ dgamma(.01, .01);	         ## "between-study" precision, prior
	sigma2 <- 1/tau;                  ## convert precision to variance
	good <- step(mu);                 ## E(good) = Pr(mu>0 | data)
	}

## data
list(y=c(2.77,2.50,1.84,2.56,2.31,-1.15),
     sd=c(1.65,1.31,2.34,1.67,1.98,0.90))

## initial values
list(mu=0,tau=1)

Results: The boost in survivorship is estimated at 1.32 percentage points with a standard deviation of .93; the posterior mean of "good" yields a Bayesian p-value for this overall treatment effect, the (posterior) probability that aspirin does not increase survivorship is 1 - .9447 = .0553.  Note that a classical analysis that simply pooled the studies yields an average treatment effect of .86 and a standard error of .59 (z = 1.47, p = .072).

	 node	 mean	 sd	 MC error	2.5%	median	97.5%	start	sample
	b[1]	0.6661	0.2461	0.007897	0.1663	0.6882	0.9953	1001	10000
	b[2]	0.5894	0.2701	0.00917	0.1117	0.5818	0.9926	1001	10000
	b[3]	0.7712	0.1995	0.005867	0.2863	0.8161	0.9977	1001	10000
	b[4]	0.67	0.2447	0.007828	0.1696	0.6933	0.9954	1001	10000
	b[5]	0.7232	0.2229	0.006835	0.2231	0.7606	0.9967	1001	10000
	b[6]	0.4634	0.2923	0.0108	0.05601	0.3963	0.9844	1001	10000
	good	0.9447	0.2286	0.004739	0.0	1.0	1.0	1001	10000
	mu	1.316	0.9346	0.02277	-0.3015	1.263	3.304	1001	10000
	sigma2	2.612	6.079	0.1084	0.01284	1.234	13.65	1001	10000
	theta[1]	1.736	1.177	0.02922	-0.2638	1.603	4.352	1001	10000
	theta[2]	1.738	1.065	0.02805	-0.06121	1.62	4.037	1001	10000
	theta[3]	1.375	1.29	0.027	-1.053	1.269	4.246	1001	10000
	theta[4]	1.655	1.176	0.02757	-0.3317	1.523	4.292	1001	10000
	theta[5]	1.538	1.247	0.02587	-0.6295	1.405	4.325	1001	10000
	theta[6]	-0.07534	0.9488	0.02355	-1.985	-0.02785	1.636	1001	10000
